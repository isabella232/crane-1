plugins {
	id 'org.springframework.boot' version '2.5.2'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
        id 'maven-publish'
}

group = 'eu.openanalytics.rdepot'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
	annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"
}

//sourceSets.main.java.outputDir = file("build/classes/java/main")

compileJava.inputs.files(processResources)

test {
	useJUnitPlatform()
}

bootJar {
    archiveClassifier = ''
    archiveVersion = version
    manifest {
        attributes( 'Implementation-Title': 'crane',
                    'Implementation-Version': version)
    }
}


publishing {
    publications {
        jarFile(MavenPublication) {
            artifact bootJar
            groupId group
            artifactId 'crane'
            version version
        }
        //jarFile(MavenPublication) {
            //artifact jar
            //groupId rootProject.group + '.crane'
            //artifactId 'crane'
            //version baseVersion
        //}
    }
    repositories {
                maven {
                        def releasesRepoUrl = "https://nexus.openanalytics.eu/repository/releases"
                        def snapshotsRepoUrl = "https://nexus.openanalytics.eu/repository/snapshots"
                        url = version.contains('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                        credentials {
                                username = System.getenv("OA_NEXUS_USER")
                                password = System.getenv("OA_NEXUS_PWD")
                        }
                }
        }
}

